plugins {
    id "java"
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id "maven-publish"
}

apply plugin: 'maven'
apply plugin: 'signing'

group = "org.dxworks"
version = "1.1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "org.junit.jupiter:junit-jupiter:5.4.2"
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    repositories {
        maven {
            name = "ossrh"

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty("ossrhUsername") as String ?: System.getenv("OSSRH_USER")
                password = project.findProperty("ossrhPassword") as String ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'argumenthor'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Argumenthor'
                description = ''
                url = 'https://github.com/dxworks/argumenthor.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'marioRivis'
                        name = 'Mario Rivis'
                        email = 'mario.rivis@gmail.com'
                    }
                    developer {
                        id = 'dariusNagy'
                        name = 'Darius Nagy'
                        email = 'nagyDarius97@gmail.com'
                    }
                }
            }
        }
    }
}
